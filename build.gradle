// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.0.0'

    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://repos.zeroturnaround.com/nexus/content/repositories/zt-public-releases' }
        mavenCentral()
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.19.2'
        classpath 'com.zeroturnaround.jrebel.android:jr-android-gradle:0.8.0.72'
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.7'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath('com.shazam.fork:fork-gradle-plugin:1.0.0') {
            exclude group: 'com.google.guava'
        }
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath "net.rdrei.android.buildtimetracker:gradle-plugin:0.5.0"
    }

}

apply plugin: "build-time-tracker"
buildtimetracker {
    reporters {
        summary {
            ordered false
            threshold 500
            barstyle "unicode"
        }
    }
}


apply plugin: 'com.github.ben-manes.versions'

// Sub-projects/library projects will refer to these top-level SDK and buildTools versions

ext.supportLibVersion = "25.0.1"
ext.googlePlayServicesVersion = "8.4.0"

ext.compileSdkVersion = 25
ext.buildToolsVersion = "25.0.1"

ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
        else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}
